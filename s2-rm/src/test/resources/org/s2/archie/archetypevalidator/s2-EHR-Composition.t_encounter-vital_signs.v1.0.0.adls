template (adl_version=2.1.0; rm_release=0.8.0)
	s2-EHR-Composition.t_encounter-vital_signs.v1.0.0

specialize
	s2-EHR-Composition.encounter.v1

language
	original_language = <[iso_639-1::en]>

description
	lifecycle_state = <"unmanaged">
	original_author = <
		["name"] = <"Nathan Davis<nathan.davis@GraphiteHealth.io>">
		["organisation"] = <"Graphite Health <http://www.GraphiteHealth.io>">
	>
	custodian_namespace = <"org.s2health">
	custodian_organisation = <"S2 Health <http://s2health.org>">
	copyright = <"Copyright (c) 2023 S2 Health">
	licence = <"Creative Commons CC-BY 4.0 unported <http://creativecommons.org/>">
	details = <
		["en"] = <
			language = <[iso_639-1::en]>
			purpose = <"Vital Signs Panel">
		>
	>

definition
	Composition[id1.1] matches {	-- Vital signs panel
		content matches {
			use_archetype Direct_observation[id0.1, s2-EHR-Direct_observation.t_vital_signs-bp.v1] occurrences matches {0..1}
			use_archetype Direct_observation[id0.2, s2-EHR-Direct_observation.t_vital_signs-hr.v1] occurrences matches {0..1}
			use_archetype Direct_observation[id0.3, s2-EHR-Direct_observation.t_vital_signs-weight.v1] occurrences matches {0..1}
			use_archetype Direct_observation[id0.4, s2-EHR-Direct_observation.height.v2] occurrences matches {0..1}
			use_archetype Direct_observation[id0.5, s2-EHR-Direct_observation.body_mass_index.v2] occurrences matches {0..1}
			use_archetype Direct_observation[id0.6, s2-EHR-Direct_observation.t_vital_signs-pulse_ox.v1.0.0] occurrences matches {0..1}
			use_archetype Direct_observation[id0.7, s2-EHR-Direct_observation.body_temperature.v2] occurrences matches {0..1}
			use_archetype Direct_observation[id0.8, s2-EHR-Direct_observation.respiratory_rate.v1] occurrences matches {0..1}
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Vital signs panel">
				description = <"Set of vital signs and other basic observations">
			>
			["id0.1"] = <
				text = <"Blood pressure">
				description = <"Vital signs BP observation">
			>
			["id0.2"] = <
				text = <"Heart rate">
				description = <"Vital signs heart rate observation">
			>
			["id0.3"] = <
				text = <"Body weight">
				description = <"Vital signs body weight observation">
			>
			["id0.4"] = <
				text = <"Height">
				description = <"Vital signs height observation">
			>
			["id0.5"] = <
				text = <"BMI">
				description = <"Vital signs BMI observation">
			>
			["id0.6"] = <
				text = <"Pulse oximetry">
				description = <"Vital signs pulse oximetry observation">
			>
			["id0.7"] = <
				text = <"Body Temperature">
				description = <"Vital signs body temperature observation">
			>
			["id0.8"] = <
				text = <"Respiratory Rate">
				description = <"Vital signs respiratory rate observation">
			>
		>
	>
	term_bindings = <
		["loinc"] = <
			["id1.1"] = <http://loinc.org/85353-1>
		>
	>

-------------------------------------------------------------
template_overlay
	s2-EHR-Direct_observation.t_vital_signs-bp.v1.0.0

specialize
	s2-EHR-Direct_observation.blood_pressure.v2


definition
	Direct_observation[id1.1] matches {	-- BP as used in vital signs panel
		/data matches {
			Node[id1008] occurrences matches {0}
		}
		/state matches {
			Node[id1044] occurrences matches {0}
			Node[id1053] occurrences matches {0}
			Node[id1006] occurrences matches {0}
		}
		/qualifiers matches {
			Node[id1011] occurrences matches {0}
			Node[id15] occurrences matches {0}
			Node[id1036] occurrences matches {0}
			use_archetype Node[id1058, s2-EHR-Node.anatomical_location.v1] occurrences matches {0}
		}
		data_series existence matches {0}
		state_series existence matches {0}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"BP as used in vital signs panel">
				description = <"BP as used in vital signs panel">
			>
		>
	>

-------------------------------------------------------------
template_overlay
	s2-EHR-Direct_observation.t_vital_signs-hr.v1.0.0

specialize
	s2-EHR-Direct_observation.heart_rate.v1


definition
	Direct_observation[id1.1] matches {	-- Heart rate as used in vital signs panel
		/data matches {
			Node[id1060] occurrences matches {0}
		}
		/state matches {
			Node[id9] occurrences matches {0}
			Node[id1053] occurrences matches {0}
			Node[id1006] occurrences matches {0}
		}
		/qualifiers matches {
			Node[id1036] occurrences matches {0}
			Node[id15] occurrences matches {0}
		}
		data_series existence matches {0}
		state_series existence matches {0}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Heart rate as used in vital signs panel">
				description = <"Heart rate as used in vital signs panel">
			>
		>
	>

-------------------------------------------------------------
template_overlay
	s2-EHR-Direct_observation.t_vital_signs-weight.v1.0.0

specialize
	s2-EHR-Direct_observation.body_weight.v2


definition
	Direct_observation[id1.1] matches {	-- Body weight as used in vital signs panel
		data_series existence matches {0}
		state_series existence matches {0}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Body weight as used in vital signs panel">
				description = <"Body weight as used in vital signs panel">
			>
		>
	>

-------------------------------------------------------------
template_overlay
	s2-EHR-Direct_observation.t_vital_signs-pulse_ox.v1.0.0

specialize
	s2-EHR-Direct_observation.pulse_oximetry.v1


definition
	Direct_observation[id1.1] matches {	-- Pulse oximetry as used in vital signs panel
		/data matches {
			Node[id45] occurrences matches {0}
			Node[id46] occurrences matches {0}
			Node[id47] occurrences matches {0}
			use_archetype Node[id55, s2-EHR-Node.waveform.v0] occurrences matches {0}
			use_archetype Node[id61, s2-EHR-Node.media_file.v1] occurrences matches {0}
		}
		/state matches {
			Node[id17] occurrences matches {0}
		}
		qualifiers existence matches {0}
		data_series existence matches {0}
		state_series existence matches {0}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Pulse oximetry as used in vital signs panel">
				description = <"Pulse oximetry as used in vital signs panel">
			>
		>
	>

