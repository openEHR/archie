-- 
-- 	component:   Graphite Reference Model BMM
-- 	description: Graphite Reference Model component formal expression. This file is an ODIN serialisation of
--                   the BMM object meta-model classes found at 
--                   https://www.openEHR.org/releases/LANG/latest/p_bmm.html
-- 	keywords:    reference model, meta-model, archetypes
-- 	author:      Thomas Beale <thomas.beale@graphitehealth.io>
-- 	support:     https://graphite.atlassian.net/issues/
-- 	copyright:   Copyright (c) 2023- Graphite Health <https://www.graphitehealth.io>
-- 	license:     Apache 2.0 <http://www.apache.org/licenses/LICENSE-2.0.html>
--

------------------------------------------------------
-- BMM version on which these schemas are based.
------------------------------------------------------
bmm_version = <"2.3">

------------------------------------------------------
-- schema identification
-- (schema_id computed as <rm_publisher>_<schema_name>_<rm_release>)
------------------------------------------------------
schema_name = <"base_patterns">
rm_publisher = <"s2">
rm_release = <"0.8.5">

------------------------------------------------------
-- schema documentation
------------------------------------------------------
schema_revision = <"0.8.5.1">
schema_lifecycle_state = <"trial">
schema_description = <"S2 Release 0.8.5 structures schema">
schema_author = <"Thomas Beale <thomas.beale@graphitehealth.io>">

------------------------------------------------------
-- inclusions
------------------------------------------------------
includes = <
	["1"] = <
		id = <"s2_base_data_types_0.8.5">
	>
	["2"] = <
		id = <"s2_base_model_support_0.8.5">
	>
>

------------------------------------------------------
-- packages
------------------------------------------------------

packages = <
	["org.s2.base.patterns"] = <
		name = <"org.s2.base.patterns">
        packages = <
            ["data_structures"] = <
                name = <"data_structures">
		        classes = <"Node", "Info_node", "Entity_ref_node", "Event", "Point_event", "Interval_event">
		    >
			["participation"] = <
				name = <"participation">
				classes = <"Participation">
			>
			["archetyped"] = <
				name = <"archetyped">
				classes = <"Pathable", "Locatable", "Info_item", "Link", "Archetyped", "Feeder_audit", "Feeder_audit_details">
			>
		>
	>
>


------------------------------------------------------
-- classes
------------------------------------------------------

class_definitions = <

	--
	--------------------- base.patterns.data_structures ------------------
	--

	["Node"] = <
		name = <"Node">
		ancestors = <"Info_item">
		is_abstract = <True>
		properties = <
			["value"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"value">
				type = <"Data_value">
			>
			["null_flavor"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"null_flavor">
				type_ref = <
					type = <"Terminology_term">
					value_constraint = <"s2.NullFlavor">
				>
			>
			["null_reason"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"null_reason">
				type = <"Text">
			>
			["items"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"items">
				type_def = <
					container_type = <"List">
					type = <"Node">
				>
			>
		>
	>

	["Info_node"] = <
		name = <"Info_node">
		ancestors = <"Node">
		properties = <
			["original_value"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"original_value">
				type = <"Data_value">
			>
		>
	>

	["Entity_ref_node"] = <
		name = <"Entity_ref_node">
		ancestors = <"Node">
		properties = <
			["is_self"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"is_self">
				type = <"Boolean">
				is_mandatory = <True>
			>
			["entity_ref"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"entity_ref">
				type = <"Object_ref">
			>
		>
	>

	["Event"] = <
		name = <"Event">
		ancestors = <"Locatable">
		is_abstract = <True>
		properties = <
			["time"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"time">
				type = <"Date_time">
				is_mandatory = <True>
				is_im_runtime = <True>
			>
			["items"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"items">
				type_def = <
					container_type = <"List">
					type = <"Node">
				>
				cardinality = <|>=1|>
			>
		>
	>

	["Point_event"] = <
		name = <"Point_event">
		ancestors = <"Event">
	>

	["Interval_event"] = <
		name = <"Interval_event">
		ancestors = <"Event">
		properties = <
			["width"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"width">
				type = <"Duration">
				is_mandatory = <True>
			>
			["sample_count"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"sample_count">
				type = <"Integer">
				is_im_runtime = <True>
			>
			["sample_function"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"sample_function">
				type = <"Sample_function_kind">
			>
		>
	>

	--
	--------------------- base.patterns.participation ------------------
	--

	["Participation"] = <
		name = <"Participation">
		ancestors = <"Locatable">
		properties = <
			["function"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"function">
				type_ref = <
					type = <"Terminology_term">
					value_constraint = <"s2.ParticipationFunction">
				>
				is_mandatory = <True>
			>
			["time"] = (P_BMM_GENERIC_PROPERTY) <
				name = <"time">
				type_def = <
					root_type = <"Interval">
					generic_parameters = <"Date_time">
				>
				is_im_runtime = <True>
			>
			["mode"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"mode">
				type_ref = <
					type = <"Terminology_term">
					value_constraint = <"s2.ParticipationMode">
				>
			>
			["performer"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"performer">
				type = <"Entity_ref_node">
				is_mandatory = <True>
			>
		>
	>

	--
	--------------------- base.model_support.archetyped ------------------
	--
	["Pathable"] = <
		name = <"Pathable">
		is_abstract = <True>
		ancestors = <"Any">
	>

	["Locatable"] = <
		name = <"Locatable">
		is_abstract = <True>
		ancestors = <"Pathable">
		properties = <
			["uid"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"uid">
				type = <"Uuid">
				is_im_infrastructure = <True>
			>
			["archetype_node_id"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"archetype_node_id">
				type = <"String">
				is_mandatory = <True>
				is_im_infrastructure = <True>
			>
			["name"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"name">
				type = <"String">
				is_mandatory = <True>
			>
			["archetype_details"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"archetype_details">
				type = <"Archetyped">
				is_im_infrastructure = <True>
			>
			["feeder_audit"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"feeder_audit">
				type = <"Feeder_audit">
				is_im_runtime = <True>
			>
		>
	>

	["Info_item"] = <
		name = <"Info_item">
		is_abstract = <True>
		ancestors = <"Locatable">
		properties = <
			["code"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"code">
				type = <"Terminology_term">
				is_im_infrastructure = <True>
			>
			["original_code"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"original_code">
				type = <"Terminology_term">
				is_im_runtime = <True>
			>
			["links"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"links">
				type_def = <
					container_type = <"List">
					type = <"Link">
				>
				cardinality = <|>=1|>
			>
		>
	>

	["Link"] = <
		name = <"Link">
		ancestors = <"Any">
		properties = <
			["meaning"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"meaning">
				type = <"Terminology_term">
				is_mandatory = <True>
			>
			["type"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"type">
				type = <"Terminology_term">
				is_mandatory = <True>
			>
			["target"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"target">
				type = <"Uri">
				is_mandatory = <True>
			>
		>
	>

	["Archetyped"] = <
		name = <"Archetyped">
		ancestors = <"Any">
		properties = <
			["archetype_id"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"archetype_id">
				type = <"Archetype_id">
				is_mandatory = <True>
				is_im_infrastructure = <True>
			>
			["template_id"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"template_id">
				type = <"Archetype_id">
				is_im_infrastructure = <True>
			>
			["rm_version"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"rm_version">
				type = <"String">
				is_mandatory = <True>
				is_im_infrastructure = <True>
			>
		>
	>

	["Feeder_audit"] = <
		name = <"Feeder_audit">
		ancestors = <"Any">
		properties = <
			["originating_system_item_ids"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"originating_system_item_ids">
				type_def = <
					container_type = <"List">
					type = <"Rwe_id_ref">
				>
				cardinality = <|>=0|>
				is_im_runtime = <True>
			>
			["feeder_system_item_ids"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"feeder_system_item_ids">
				type_def = <
					container_type = <"List">
					type = <"Rwe_id_ref">
				>
				cardinality = <|>=0|>
				is_im_runtime = <True>
			>
			["original_content"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"original_content">
				type = <"Encapsulated">
				is_im_runtime = <True>
			>
			["originating_system_audit"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"originating_system_audit">
				type = <"Feeder_audit_details">
				is_mandatory = <True>
				is_im_runtime = <True>
			>
			["feeder_system_audit"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"feeder_system_audit">
				type = <"Feeder_audit_details">
				is_im_runtime = <True>
			>
		>
	>

	["Feeder_audit_details"] = <
		name = <"Feeder_audit_details">
		ancestors = <"Any">
		properties = <
			["system_id"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"system_id">
				type = <"String">
				is_mandatory = <True>
				is_im_runtime = <True>
			>
			["location"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"location">
				type = <"Entity_ref_node">
				is_im_runtime = <True>
			>
			["provider"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"provider">
				type = <"Entity_ref_node">
				is_im_runtime = <True>
			>
			["subject"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"subject">
				type = <"Entity_ref_node">
				is_im_runtime = <True>
			>
			["time"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"time">
				type = <"Date_time">
				is_im_runtime = <True>
			>
			["version_id"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"version_id">
				type = <"String">
				is_im_runtime = <True>
			>
			["other_details"] = (P_BMM_INDEXED_CONTAINER_PROPERTY) <
				name = <"other_details">
				type_def = <
					container_type = <"Hash">
					index_type = <"String">
					type = <"String">
				>
			>
		>
	>

>
