apply plugin: 'maven-publish'
if(gradle.ext.shouldSign) {
  apply plugin: 'signing'
  signing {
    afterEvaluate { project ->
      useGpgCmd()
      sign publishing.publications
    }
  }
}
//define the repositories on global level, not per project, or you get many of them
publishing {
  repositories {
    maven {
      name = "ossrh"
      //  if(gradle.ext.shouldSign) {
      //    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      //  }
      // change URLs to point to your repos, e.g. http://my.org/repo
      def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
      def snapshotsRepoUrl = 'ttps://oss.sonatype.org/content/repositories/snapshots/'
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

      credentials {
        username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
        password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
      }
    }
  }
}


//now uploading will have to be configured on a per subproject basis
subprojects {

  publishing {

    publications {
      "$project.name"(MavenPublication) {
        afterEvaluate {
          from components.java
          artifact sourcesJar
          artifact javadocJar
          artifactId = project.name
          pom {
            name = project.name
            description = project.description
          }
        }

        pom {

          url = "https://github.com/openEHR/archie"
          organization {
            name = "Nedap Healthcare"
            url = "http://www.nedap-healthcare.com"
          }
          licenses {
            license {
              name = "Apache License, Version 2.0"
              url = "http://www.apache.org/licenses/LICENSE-2.0"
              distribution = "repo"
            }
          }
          scm {
            url = "https://github.com/openEHR/archie"
            connection = "scm:git:git://github.com/openEHR/archie"
            developerConnection = "scm:git:git://github.com/openEHR/archie"
          }
          developers {
            developer {
              id = "pieterbos"
              name = "Pieter Bos"
              email = "pieter.bos@nedap.com"
            }
            developer {
              id = "tanja.dejong"
              name = "Tanja de Jong"
              email = "tanja.dejong@nedap.com"
            }
          }
          issueManagement {
            system = "Github"
            url = "https://github.com/openEHR/archie/issues"
          }
        }
      }
    }
  }

}
